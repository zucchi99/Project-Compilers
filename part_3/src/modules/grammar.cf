
-- comment: one argument for single-line comments
comment "//";
-- comment: two arguments for multi-line comments
comment "{" "}"; 
comment "(*" "*)";

-- entrypoints: set of initial states of the grammar
entrypoints Program;

-- __________ PASCAL KEYWORDS __________

-- program definition
ProgramStart.                   Program      ::= "program" Ident ";" BlockWithDecl "." ;

-- __________ BLOCK STRUCTURE __________

-- block structure with declarations
BlockWithDeclaration.       BlockWithDecl ::= [Declaration] BlockExec ;
separator Declaration "" ;

-- Useful, because inside a begin ... end block there can be a nested block, only without declarations
BlockOnlyExecution.         BlockExec     ::= "begin" NonMandatoryTerminator [Statement] "end" ;
separator Statement ";" ;

rules                            NonMandatoryTerminator ::= {- empty -} | ";" ;

-- variables, functions, procedures
rules                           Declaration   ::= CostantsBlock | VariablesBlock | FunctionForw | ProcedureForw | FunctionDecl | ProcedureDecl ; --Declaration

-- __________ COSTANTS DECLARATIONS __________

-- costants block declaration (cannot be empty)
rules                           CostantsBlock ::= "const" [ConstantDecl] ; -- CostantsBlock
terminator nonempty ConstantDecl ";" ;
ConstantDeclaration.            ConstantDecl  ::= Ident "=" RightExp ;

-- __________ VARIABLES DECLARATIONS __________

-- variables block declaration (cannot be empty)
rules                           VariablesBlock ::= "var" [VariableDeclBlock] ; -- VariablesBlock. 

-- if i use "var" I need to declare at least 1 variable
terminator nonempty VariableDeclBlock  ";" ;
-- Declaring a function, i don't want always to declare a variable
separator VariableDeclFunc  ";" ;

-- Variables Declaration inside var block
VariableDeclarationInsideBlock. VariableDeclBlock   ::= [Ident] ":" Type InitAssign ;
rules                           InitAssign          ::= {- empty -} | "=" RightExp ;       
 
-- Variables Declaration inside function parenthesis 
-- Exist the possibility to declare a function without paranthesis, if it has no parameters
rules                           DeclarationFunc     ::= {- empty -} | "(" [VariableDeclFunc] ")" ;
VariableDeclarationInsideF.     VariableDeclFunc    ::= [Ident] ":" Type ; 
separator nonempty Ident "," ;

-- __________ FUNCTION AND PROCEDURE DECLARATIONS __________

-- function and procedure declaration
FunctionSignature.              FunctionSign    ::= "function" Ident DeclarationFunc ":" Type ";" ;
ProcedureSignature.             ProcedureSign   ::= "procedure" Ident DeclarationFunc ";" ;
FunctionDeclaration.            FunctionDecl    ::= FunctionSign BlockWithDecl ";" ;
ProcedureDeclaration.           ProcedureDecl   ::= ProcedureSign BlockWithDecl ";" ;
FunctionForward.                FunctionForw    ::= FunctionSign "forward" ";" ;
ProcedureForward.               ProcedureForw   ::= ProcedureSign "forward" ";" ;

FunctionCall.                   FunctionCall   ::= Ident "(" [RightExp] ")" ;
ProcedureCall.                  ProcedureCall  ::= Ident "(" [RightExp] ")" ;
separator RightExp "," ;

-- __________ STATEMENTS __________

StatementBlock.                 Statement       ::= BlockExec ;
StatementIf.                    Statement       ::= "if" RightExp "then" Statement ElseBlock ;
StatementFor.                   Statement       ::= "for" Assign "to" RightExp "do" Statement ;
StatementWhile.                 Statement       ::= "while" RightExp "do" Statement ;
StatementRepeatUntil.           Statement       ::= "repeat" [Statement] "until" RightExp ;
StatementAssign.                Statement       ::= Assign ;
StatementFunctionCall.          Statement       ::= FunctionCall ;
StatementProcedureCall.         Statement       ::= ProcedureCall ;
StatementWrite.                 Statement       ::= WritePrimitive ;
StatementRead.                  Statement       ::= ReadPrimitive ;

rules                           ElseBlock       ::= {- empty -} | "else" Statement ; --ElseDefinition

VariableAssignment.             Assign          ::= LeftExp ":=" RightExp;

-- __________ RIGHT EXPRESSIONS __________

coercions RightExp 7 ;
RightExpOr.                     RightExp        ::= RightExp "or" RightExp1 ;

RightExpAnd.                    RightExp1       ::= RightExp1 "and" RightExp2 ;

RightExpGreater.                RightExp2       ::= RightExp2 ">" RightExp3 ;
RightExpLess.                   RightExp2       ::= RightExp2 "<" RightExp3 ;
RightExpGreaterEqual.           RightExp2       ::= RightExp2 ">=" RightExp3 ;
RightExpLessEqual.              RightExp2       ::= RightExp2 "<=" RightExp3 ;
RightExpEqual.                  RightExp2       ::= RightExp2 "=" RightExp3 ;

RightExpPlus.                   RightExp3       ::= RightExp3 "+" RightExp4 ;
RightExpMinus.                  RightExp3       ::= RightExp3 "-" RightExp4 ;

RightExpTimes.                  RightExp4       ::= RightExp4 "*" RightExp5 ;
RightExpDivide.                 RightExp4       ::= RightExp4 "/" RightExp5 ;
RightExpMod.                    RightExp4       ::= RightExp4 "mod" RightExp5 ;
RightExpDiv.                    RightExp4       ::= RightExp4 "div" RightExp5 ;

RightExpPower.                  RightExp5       ::= RightExp5 "**" RightExp6 ;

RightExpNot.                    RightExp6       ::= "not" RightExp7 ;
RightExpMinusUnary.             RightExp6       ::= "-" RightExp7 ;
RightExpPlusUnary.              RightExp6       ::= "+" RightExp7 ;

RightExpInteger.                RightExp7       ::= Integer ;
RightExpReal.                   RightExp7       ::= Double ;
RightExpBoolean.                RightExp7       ::= Boolean ;
RightExpChar.                   RightExp7       ::= Char ;
RightExpString.                 RightExp7       ::= String ;
RightExpFunctionCall.           RightExp7       ::= FunctionCall ;
RightExpCopy.                   RightExp7       ::= LeftExp ;

-- __________ LEFT EXPRESSIONS __________

LeftExpIdent.                   LeftExp         ::= Ident ;
LeftExpArrayAccess.             LeftExp         ::= LeftExp "[" [RightExp] "]" ;
LeftExpPointerValue.            LeftExp         ::= LeftExp "^" ;
LeftExpPointerAddress.          LeftExp         ::= LeftExp "@" ;

-- __________ TYPES __________

-- basic types definition
rules                           Type            ::= BaseType | CompositeType ; --AnyType
rules                           BaseType        ::= "integer" | "real" | "char" | "boolean" | "string" ; --BaseType
rules                           Boolean         ::= "true" | "false" ;

-- complex types definition
-- actually for arrays should be: array [0..5, 1..6] if multi-dimensional
CompTypeArray.                  CompositeType   ::= "array" "[" [ArrayDeclarationDim] "]" "of" BaseType ;
separator ArrayDeclarationDim "," ;
-- The declaration with variable length is not allowed in Pascal, but here it's allowed
-- we need to be sure that the right expression produce an integer
ArrayDeclarationDim.            ArrayDeclarationDim     ::= RightExp ".." RightExp ;

-- Pointer of pointer in Pascal is not allowed, but here is allowed, BECAUSE THIS IS NOT PASCAL
CompTypePointer.                CompositeType   ::= "^" Type ;

-- __________ READ AND WRITE __________

WriteInt.                       WritePrimitive  ::= "writeInt" "(" RightExp ")" ;
WriteReal.                      WritePrimitive  ::= "writeReal" "(" RightExp ")" ;
WriteChar.                      WritePrimitive  ::= "writeChar" "(" RightExp ")" ;
WriteString.                    WritePrimitive  ::= "writeString" "(" RightExp ")" ;
ReadInt.                        ReadPrimitive   ::= "readInt" "(" LeftExp ")" ;
ReadReal.                       ReadPrimitive   ::= "readReal" "(" LeftExp ")" ;
ReadChar.                       ReadPrimitive   ::= "readChar" "(" LeftExp ")" ;
ReadString.                     ReadPrimitive   ::= "readString" "(" LeftExp ")" ;
